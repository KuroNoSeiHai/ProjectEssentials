buildscript {
    repositories {
        maven { url("https://files.minecraftforge.net/maven") }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath(
            group: "net.minecraftforge.gradle",
            name: "ForgeGradle",
            version: forge_gradle_version,
            changing: true
        )
        classpath(
            group: "org.jetbrains.kotlin",
            name: "kotlin-gradle-plugin",
            version: kotlin_version
        )
        classpath(
            group: "org.jetbrains.kotlin",
            name: "kotlin-serialization",
            version: kotlin_version
        )
    }
}

apply(plugin: "net.minecraftforge.gradle")
apply(plugin: "kotlin")
apply(plugin: "kotlinx-serialization")

version = module_version
group = "com.mairwunnx.$module_id"
archivesBaseName = module_name

minecraft {
    mappings(
        channel: forge_mappings_channel_type,
        version: forge_mappings_channel_version
    )

    runs {
        client {
            //noinspection GroovyAssignabilityCheck
            workingDirectory(project.file(dev_run_client_path))
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
            mods {
                ProjectEssentials {
                    //noinspection GroovyAssignabilityCheck
                    source(sourceSets.main)
                }
            }
        }

        server {
            //noinspection GroovyAssignabilityCheck
            workingDirectory(project.file(dev_run_server_path))
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
            mods {
                ProjectEssentials {
                    //noinspection GroovyAssignabilityCheck
                    source(sourceSets.main)
                }
            }
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url("https://jitpack.io") }
}

dependencies {
    minecraft(group: "net.minecraftforge", name: "forge", version: forge_version)
    compile(group: "com.mojang", name: "brigadier", version: brigadier_version)
    compile(
        group: "org.jetbrains.kotlinx",
        name: "kotlinx-serialization-runtime",
        version: kotlinx_serialization_version
    )
    compile(
        group: "org.jetbrains.kotlin",
        name: "kotlin-stdlib-$kotlin_jdk_version_target",
        version: kotlin_version
    )
}

jar {
    manifest {
        attributes([
            "Specification-Title"     : module_name,
            "Specification-Vendor"    : module_vendor,
            "Specification-Version"   : module_version,
            "Implementation-Title"    : module_name,
            "Implementation-Version"  : module_version,
            "Implementation-Vendor"   : module_vendor,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

sourceCompatibility = targetCompatibility =
    compileJava.sourceCompatibility =
        compileJava.targetCompatibility = project_jvm_version_target

compileKotlin.kotlinOptions.jvmTarget =
    compileTestKotlin.kotlinOptions.jvmTarget = project_jvm_version_target

//noinspection GroovyAssignabilityCheck
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
    }
}
