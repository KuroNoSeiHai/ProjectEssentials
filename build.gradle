buildscript {
    repositories {
        maven { url("https://files.minecraftforge.net/maven") }
        maven { url("https://libraries.minecraft.net") }
        maven { url("https://plugins.gradle.org/m2/") }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath(
            group: "net.minecraftforge.gradle",
            name: "ForgeGradle",
            version: forgeGradleVersion,
            changing: true
        ) {
            exclude(group: "trove", module: "trove")
        }
        classpath(
            "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        )
    }
}

apply(plugin: "net.minecraftforge.gradle")
apply(plugin: "kotlin")

version = projectEssentialsVersion
group = "com.mairwunnx.$projectEssentialsId"
archivesBaseName = projectEssentialsName

configurations {
    modDependencies
}

minecraft {
    mappings(
        channel: mappingsChannelType,
        version: mappingsChannelVersion
    )

    runs {
        client {
            workingDirectory(project.file(devRunClientPath))
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
            mods {
                ProjectEssentials {
                    source(sourceSets.main)
                }
            }
        }

        server {
            workingDirectory(project.file(devRunServerPath))
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
            mods {
                ProjectEssentials {
                    source(sourceSets.main)
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url("https://minecraft.curseforge.com/api/maven/") }
    maven { url("https://libraries.minecraft.net") }
}

dependencies {
    minecraft(group: "net.minecraftforge", name: "forge", version: forgeBaseVersion)
    compile(group: "com.mojang", name: "brigadier", version: brigadierBaseVersion)
    compile(group: "kottle", name: "Kottle", version: kottleBaseVersion)
    modDependencies(group: "kottle", name: "Kottle", version: kottleBaseVersion)
    compile(
        group: "org.jetbrains.kotlin",
        name: "kotlin-stdlib-$kotlinJdkVersionTarget",
        version: kotlinVersion
    )
}

task installMods(type: Copy, dependsOn: "deinstallMods") {
    from { configurations.modDependencies }
    include("**/*.jar")
    into(file("$devRunClientPath/mods"))
    into(file("$devRunServerPath/mods"))
}

task deinstallMods(type: Delete) {
    delete(fileTree(dir: "$devRunClientPath/mods", include: "*.jar"))
    delete(fileTree(dir: "$devRunServerPath/mods", include: "*.jar"))
}

project.afterEvaluate {
    project.tasks["prepareRuns"].dependsOn(project.tasks["installMods"])
}

jar {
    manifest {
        attributes([
            "Specification-Title"     : projectEssentialsName,
            "Specification-Vendor"    : projectEssentialsVendor,
            "Specification-Version"   : projectEssentialsVersion,
            "Implementation-Title"    : projectEssentialsName,
            "Implementation-Version"  : projectEssentialsVersion,
            "Implementation-Vendor"   : projectEssentialsVendor,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

sourceCompatibility = targetCompatibility =
    compileJava.sourceCompatibility =
        compileJava.targetCompatibility = jvmVersionTarget

compileKotlin.kotlinOptions.jvmTarget =
    compileTestKotlin.kotlinOptions.jvmTarget = jvmVersionTarget
